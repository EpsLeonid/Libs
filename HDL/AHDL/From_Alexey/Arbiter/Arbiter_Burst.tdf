% This Function is BUS Controller (Access Arbiter) for T2Q Project Internal Bus.
This function ONLY regulate how Master device asserts address on Address Lines. Read or Write 
data thorough Data Lines is under Master control. Target devices has to decode address and accept/supply data.  
When Target is READY for transfer, Data Strobe signal is generated to complete the cycle.
%
TITLE "Arbiter with burst mode";
-- Ver4 5.03.2019

%
This Asyncronouse Interface is intended for data tranfser between devices with different speed of Clock.

Bus access is organized in some steps:
1. Master Device asserts "Bus Access ReQuiest".
2. At positive transitions of CLK supplied to Arbiter "Bus Access GranteD" occuried.
If more than one requiest found, high priority device will be granted first.
3. Master Device which is granted, opens tri-state buffers and thus asserts "Adderess Lines" and "Bus Direction" .
4. At next positive transition of CLK "Address Valid" signal asserted by Arbiter. 
5. Now it's some WaiteStates inserted to address decode by Targets'.
6. When any Target Device recognize it's address, (Address decoded), It asserts signal "Target Ready".
7. At positive transitions of CLK signal "Target Ready" sampled and, if present, 
signal "Data Strobe" generated. "Data Strobe" is 1T of CLK, negative transitions
is the end of transfer cycle.
P.S. 
1. If "Target Ready" not asserted while ClockMaxTimout expered, "Data Strobe" 
will be produced to clear current cycle, "Error" signal generated.
2. If device granted does not remove request after bus cycle completed it 
will be ignored for 1 clock cycle.
This features are to be used at initial system testing, ajusting.
%


INCLUDE "lpm_counter.inc"  ;
INCLUDE "Edge_Sensing_Sync.inc";

PARAMETERS
(
  DeviceMaxNumber = 2,   -- Number of Master Devices connected to Bus
  ClockMaxTimout  = 3,  -- Maximum clock number until Target Ready
  Burst_Max_Cycles  = 4096 -- maximum number of strobes in DMA mode
);
ASSERT (ClockMaxTimout>1) and (ClockMaxTimout<60)
	report "Arbiter TimeOut should be 1..60" ;

Constant    Burst_Cnt_SIZE    =	floor(log2(Burst_Max_Cycles));

	 
Subdesign Arbiter_Burst
(
  clock                            		: input;  -- System clock
  Reset                            		: input;  -- Active High

-- HandShake Begin
-- Requestes from Master devices to bus access to 
  BARQ[DeviceMaxNumber-1..0]       			: input; 

-- Bus Access Granted 
  BAGD[DeviceMaxNumber-1..0]       			: output;

--Negotiation signals
  AddressValid                     			: output; -- Target Address Decode
  TargetReady                      			: input;  -- Target Device Ready when addr decoded
  DataStrobe                       			: output; -- One clock HIGH when Target Ready
-- End of HandShake

  ErrorLight                       			: output; -- One clock HIGH when Strobe at Timout 
  
  Burst_number_cycles[Burst_Cnt_SIZE-1..0]	: input = GND;
  Burst_Cycle_END_Out						: output;
  Burst_Pause								: input = GND;
  
  
 --**************************** 
  Burst_number_cycles_VP[Burst_Cnt_SIZE..0] : output;
  End_Cycle_VP,Mask_Strobe_VP				: output;
  Burst_Cycles_CNT_VP[Burst_Cnt_SIZE..0]	: output;

)

VARIABLE

  BARQ_inBuff[DeviceMaxNumber-1..0]         : DFFE ; -- Input sampling
  BAGD_outBuff[DeviceMaxNumber-1..0]        : DFFE ; -- Output strobed (optional)
  Mask_Buff[DeviceMaxNumber-1..0]           : DFFE ; -- Register_of_Mask

  BARQ_OR, BARQ_OR_[DeviceMaxNumber..0]     : node;
  BAGD_OR, BAGD_OR_[DeviceMaxNumber..0]     : node;
  wire_AND[DeviceMaxNumber-2..0]			: node;                              

  End_Cycle								    : node;
  TimeOutCnt                                : LPM_COUNTER WITH (LPM_WIDTH = 6);
  TimeOut									: NODE;
  
  Burst_Cycles_CNT							: LPM_COUNTER WITH (LPM_WIDTH = Burst_Cnt_SIZE+1);
  Burst_Stobes								: NODE;
  
  Mask_Strobe								: NODE;
  Burst_Cycle_END							: NODE;

BEGIN

Defaults 
    wire_AND[] = VCC;  -- aiii?aaaeyai yaii ai eiae?aneie 1
END Defaults;

-- Bus connection begin			
  FOR i IN 0 TO (DeviceMaxNumber-1) GENERATE
     BARQ_inBuff[i].d     = BARQ[i]; 
     BARQ_inBuff[i].clk   = clock;
     BARQ_inBuff[i].clrn  = !Mask_Buff[i].q;

	 BAGD[i]              = BAGD_outBuff[i].q;
	 BAGD_outBuff[i].clk  = clock;
	 BAGD_outBuff[i].clrn = !End_Cycle;

	 Mask_Buff[i].d       = BAGD_outBuff[i].q;
	 Mask_Buff[i].clk     = clock;
	 Mask_Buff[i].ENA     = Mask_Strobe;
	 Mask_Buff[i].clrn    = !End_Cycle ;
  END GENERATE;

-- Priority Encoder to ensure single active BAGD
  FOR j IN 0 TO (DeviceMaxNumber-2) GENERATE
     wire_AND[j] = BARQ_inBuff[j].q;
     FOR i IN j TO (DeviceMaxNumber-2) GENERATE
        wire_AND[j] = !BARQ_inBuff[i+1].q;
     END GENERATE;
     IF wire_AND[j] == VCC THEN BAGD_outBuff[j].d = VCC; 
                           ELSE BAGD_outBuff[j].d = GND;
     END IF;
  END GENERATE;

  IF   (BARQ_inBuff[DeviceMaxNumber-1].q == VCC) THEN BAGD_outBuff[DeviceMaxNumber-1].d = VCC;
												 ELSE BAGD_outBuff[DeviceMaxNumber-1].d = GND; 
  END IF; 
-- Bus connection end

-- Input sampling control
  BARQ_OR_[0] = GND;
  FOR i IN 0 TO (DeviceMaxNumber-1) GENERATE
     BARQ_OR_[i+1] = BARQ_OR_[i] OR BARQ_inBuff[i];
     BARQ_inBuff[i].ena=!BARQ_OR; -- IF any request exist, stop next sampling
  END GENERATE;
  BARQ_OR = BARQ_OR_[DeviceMaxNumber];

-- Output control
  BAGD_OR_[0] = GND;
  FOR i IN 0 TO (DeviceMaxNumber-1) GENERATE
     BAGD_OR_[i+1] = BAGD_OR_[i] OR BAGD_outBuff[i];
  END GENERATE;
  BAGD_OR = BAGD_OR_[DeviceMaxNumber];

  AddressValid	=	DFF(.d=BAGD_OR, .clk=Clock); -- AddressValid is one clock late of BAGD

-- From "Target Ready" to "Data Strobe" control--Shaping one clock width pulse from positive edge of Target Ready
  End_Cycle   = DFF(.d=Burst_Cycle_END, .clk = Clock);
  --End_Cycle   = DFF(.d=((Burst_Cycles_CNT.q[]>=(Burst_number_cycles[],GND)) AND (TargetReady OR TimeOut)), .clk = Clock);
  
  DataStrobe  = Burst_Stobes;
  Mask_Strobe = Burst_Cycle_END;


-- Time_Out Timer: Starts  on "Target Ready" , Cleared at next clock of DataStrobe
  TimeOutCnt.(clock, sclr, cnt_en) = (clock, TargetReady OR Burst_Cycle_END, AddressValid AND !TimeOut);-- Count enabled when Address Valid
  TimeOut	=	DFF (.d=(TimeOutCnt.q[] >= ClockMaxTimout ), .clk=clock);
  ErrorLight = TimeOut;  -- Error marker for external light
  
-- DMA control 
  Burst_Cycles_CNT.(clock, cnt_en, sclr) = (clock, (TargetReady OR TimeOut) AND !Burst_Pause, (Burst_Cycles_CNT.q[]>=(Burst_number_cycles[],GND)) AND (TargetReady OR TimeOut) %Burst_Cycle_END OR End_Cycle%);
  Burst_Stobes = DFF(.d=Burst_Cycles_CNT.q[0], .clk = clock);
  Burst_Cycle_END = DFF(.d=(Burst_Cycles_CNT.q[]>=(Burst_number_cycles[],GND)-1) AND (TargetReady OR TimeOut), .clk = clock,.clrn=!End_Cycle); 
  Burst_Cycle_END_Out = Burst_Cycle_END;
  
 
  End_Cycle_VP	= End_Cycle;--Data_Strobe_Shaper.q OR TimeOut;
  Mask_Strobe_VP = (TargetReady OR TimeOut);--Mask_Strobe;
  Burst_Cycles_CNT_VP[] = Burst_Cycles_CNT.q[]; 
  Burst_number_cycles_VP[] = (Burst_number_cycles[],GND);


END;